
<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<digits> ::= <digit>
            | <digits> <digit>
<character> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P"  | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
<characters> ::= <character>
            | <characters> <character>

<digit-or-character> ::= <digit>
            | <character>
<digits-and-characters> ::= <digit-or-character> | <digits-and-characters> <digit-or-character>

<symbol> ::= "|" | " " | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | ";" | ">" | "=" | "<" | "?" | "@" | "[" | "\\" | "]" | "^" | "_" | "'" | "\'" | "{" | "}" | "~" | "\n" | "\r" | "\t" | "\""
<symbols> ::= <symbol>
            | <symbols> <symbol>
<text> ::= <digit>
            | <character>
            | <symbol>
<all-text-symbols> ::= <text> | <all-text-symbols> <text>

<identifier-and-function-call> ::= <identifier>
			| <identifier> "(" ")"
			| <identifier> "(" <call-args> ")"

<constant> ::= <digits>
            | <digits> "." <digits>
            | <digits> "."
            | "." <digits>
			| <identifier-and-function-call>

<string> ::= "'" <all-text-symbols> "'"
            | "\"" <all-text-symbols> "\""

<factor> ::= "(" <unary-expression> ")""
            | <constant>

<term> ::= <term> "*" <factor>
            | <term> "/" <factor>
            | <factor>

<math> ::= <math> "+" <term>
            | <math> "-" <term>
            | <term>

<relational-exp> ::= <math> <relational-symbol> <math>
            | <math>

<expression> ::= <relational-exp>
            | <relational-exp> <logical-symbol> <unary-expression>

<unary-expression> ::= <expression>
            | <unary-symbol> <unary-expression>

<unary-symbol> ::= "-"
			| "!"
			
<logical-symbol> ::= "&&"
            | "||"

<relational-symbol> ::= "=="
            | ">="
            | "<="
            | "!="
            | ">"
            | "<"

<type> ::= "int"
            | "string"
            | "float"
<identifier> ::= <digits-and-characters>
<type-and-ident> ::= <type> <identifier>




	
			
			
<dec-arg> ::= <unary-expression>
            | <variable-declaration>
            | <identifier>
<dec-args> ::= <dec-arg>
            | <dec-args> "," <dec-arg>
<funtion-declaration-args-and-block> ::= "(" <dec-args> ")" <statement-region>
			| "(" ")" <statement-region>
<variable-function-declaration> ::=	 <type-and-ident> <funtion-declaration-args-and-block>
			| <type-and-ident> ";"
			| <type-and-ident> "=" <unary-expression> ";"
			| <type-and-ident> "=" <string> ";"
		
		
		
		
		
<call-args> ::= <unary-expression>
            | <unary-expression> "," <unary-expression>	
<function-call-args> ::= "(" <call-args> ")" ";"
			| "(" ")" ";"
<function-call-variable-assignment> ::= <identifier> <function-call-args>
			| <identifier> "=" <unary-expression> ";"
			| <identifier> "=" <string> ";"
			
			
			

<jump-statement> ::= "nutraukti" ";"
            | "testi" ";"
			| "grazinti" ";"
			| "grazinti" <unary-expression> ";"
			
				


<statement-region> ::= ":" "(" <statements> ")"
					 | ":" "(" ")"
<pakolei-statement> ::= "pakolei" "(" <unary-expression> ")" <statement-region>
<kitaip-statement> ::= "kitaip" <statement-region>
<kitaip-jei-statement> ::= "kitaip-jei" "(" <unary-expression> ")" <statement-region>
            | "kitaip-jei" "(" <unary-expression> ")" <statement-region> <kitaip-jei-statement>
            | "kitaip-jei" "(" <unary-expression> ")" <statement-region> <kitaip-statement>
<jei-statement> ::= "jei" "(" <unary-expression> ")" <statement-region>
            | "jei" "(" <unary-expression> ")" <statement-region> <kitaip-jei-statement>
            | "jei" "(" <unary-expression> ")" <statement-region> <kitaip-statement>

<statement> ::= <jei-statement>
            | <pakolei-statement>
			| <jump-statement>
			| <function-call-variable-assignment>
			| <variable-function-declaration>
<statements> ::= <statement>
            | <statements> <statement>
<start> ::= <statements>
